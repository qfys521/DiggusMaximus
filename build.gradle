plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+mc${project.minecraft_version}"
group = project.maven_group

repositories {
	maven { url 'https://maven.fabricmc.net' }
	maven { url 'https://maven.yuluo.dev/repository/maven-public' }
	maven { url 'https://maven.terraformersmc.com/releases' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation  "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation  "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation(include("net.kyrptonaught:kyrptconfig:${kyrptconfig_version}")) {
		transitive false
	}

	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}") {
		transitive false
	}
}

processResources {
	var replaceTokens = [
			'version'          : project.mod_version,
			'minecraft_version': project.minecraft_version,
			'loader_version'   : project.loader_version
	]

	inputs.properties replaceTokens
	filteringCharset 'UTF-8'

	filesMatching('fabric.mod.json') {
		expand replaceTokens
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

publishing {
	repositories {
		mavenLocal()

		maven {
			name = 'YuluoMaven'

			def ENV = System.getenv()
			def releaseUrl = 'https://maven.yuluo.dev/repository/maven-releases/'
			def snapshotUrl = 'https://maven.yuluo.dev/repository/maven-snapshots/'
			url = ENV.MOD_RELEASE ? releaseUrl : snapshotUrl

			credentials {
				username ENV.MOD_MAVEN_USER
				password ENV.MOD_MAVEN_PASS
			}
		}
	}

	publications {
		mavenFabric(MavenPublication) {
			artifactId = project.name
			version = "true".equalsIgnoreCase(System.getenv("MOD_RELEASE")) ? version : "${version}-SNAPSHOT"
			from components.java
		}
	}
}
